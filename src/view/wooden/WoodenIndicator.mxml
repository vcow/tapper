<?xml version="1.0"?>
<f:LayoutGroup xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:f="library://ns.feathersui.com/mxml"
               backgroundSkin="{new Image(atlas.getTexture('wooden_indicator'))}"
               initialize="updateDigits()">
    <f:layout>
        <f:HorizontalLayout gap="2" paddingTop="13" paddingLeft="14"/>
    </f:layout>
    <fx:Script><![CDATA[
        import resources.AtlasLibrary;

        import starling.display.Image;
        import starling.textures.Texture;
        import starling.textures.TextureAtlas;

        private var _digits:Vector.<Image> = new Vector.<Image>(12, true);
        private var _value:Number;

        private static var _atlas:TextureAtlas;
        private static function get atlas():TextureAtlas
        {
            if (!_atlas)
            {
                _atlas = AtlasLibrary.getInstance().manager.getTextureAtlas("wooden");
            }
            return _atlas;
        }

        override protected function initialize():void
        {
            super.initialize();

            var texture:Texture = atlas.getTexture("space");
            for (var i:int = 0; i < 12; i++) {
                var image:Image = new Image(texture);
                _digits[i] = image;
                addChild(image);
            }
        }

        public function set value(value:Number):void
        {
            value = Math.floor(value);
            if (value == _value) return;
            _value = value;
            if (isInitialized) updateDigits();
        }

        private function updateDigits():void
        {
            var digits:Array = isNaN(_value) ? [] : _value.toString().split("");
            for (var i:int = 11, j:int = digits.length - 1; i >= 0; i--, j--) {
                var texture:Texture = j < 0 ? atlas.getTexture("space") : atlas.getTexture(digits[j]);
                if (texture) _digits[i].texture = texture;
            }
        }
        ]]></fx:Script>
</f:LayoutGroup>
